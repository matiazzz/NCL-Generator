grammar ar.edu.unq.tvd.nclgenerator.NCLGenerator with org.eclipse.xtext.common.Terminals

generate nCLGenerator "http://www.edu.ar/unq/tvd/nclgenerator/NCLGenerator"

NCL:
	'name:' name=STRING
	regions+=Region*
    medias+=Media*
;

Media:
    'media' name=ID '{'
    ('src' '=' src=STRING | 'type:' type=STRING)
    (('region' '=' region=[Region]) | regionProperties+=RegionProperty*)
    (properties+=MediaProperty)*
	(start ?= 'start')?
    (conditionActions+=CondtionAction)*
    '}'
;

MediaProperty:
	'property' name=ID '=' value=STRING
;

RegionProperty:
	name=regionProperties '=' value=INT
;

Region:
	'region' name=ID  '{'
		regionProperties+=RegionProperty*
	'}'
;

CondtionAction:
 'event' condition=Condition '->' (simpleAction=SimpleAction | compoundAction=CompoundAction)
;

SimpleAction:
	action=Action ('('media=[Media]')')?
;

CompoundAction: {CompoundAction}
	operator=Operator '['(simpleAction+=SimpleAction)*']'
;

enum Operator:
	seq | par
;

enum Action:
	 start | stop | set | abort | resume | pause
;

enum regionProperties: 
	width | height | top | bottom | right | left | title | zIndex
;

Condition:
	OnSelection | OnStart | OnStop
;

OnSelection:
	'onSelection' '('key=Key')'
;

OnStart:
	'onStart' '('media=[Media]')'
;

OnStop:
	'onStop' '('media=[Media]')'
;

enum Key:
	BLUE | RED | GREEN | YELLOW | CURSOSR_DOWN | CURSOR_UP | CURSOR_LEFT | CURSOR_RIGHT //etc 
;