grammar ar.edu.unq.tvd.nclgenerator.NCLGenerator with org.eclipse.xtext.common.Terminals

generate nCLGenerator "http://www.edu.ar/unq/tvd/nclgenerator/NCLGenerator"

NCL:
	'name:' name=STRING
	regions+=Region*
    medias+=Media*
;

Media:
    'media' name=ID '{'
    ('src' '=' src=STRING | 'type:' type=STRING)
    (('region' '=' region=[Region]) | regionProperties+=RegionProperty*)
    ('property' properties+=MediaProperty)*
	(start ?= 'start')?
    (conditionActions+=CondtionAction)*
    '}'
;

MediaProperty:
	name=ID '=' value=STRING
;

RegionProperty:
	name=ID '=' value=INT
;

Region:
	'region' name=ID  '{'
		regionProperties+=RegionProperty*
	'}'
;

CondtionAction:
 'event' condition=Condition '->' (simpleAction=SimpleAction | compoundAction=CompoundAction)
;

SimpleAction:
	action=Action (media=[Media])?
;

CompoundAction: {CompoundAction}
	operator=Operator '['(simpleAction+=SimpleAction)*']'
;

Condition:
	OnSelection | OnAction
;

OnSelection:
	'onSelection' key=Key
;

OnAction:
	condition=conditions (media=[Media])?
;

Key:
	name=ID
;

enum Operator:
	seq | par
;

enum Action:
	 start | stop | set | abort | resume | pause
;

enum conditions:
	onStart | onEnd | onBegin | onPause | onResume | onAbort
;